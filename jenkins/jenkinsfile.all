#!groovy

//
// Use this to set version of casual
//

//
// Extract current version
//
def retrieve_major_minor()
{
   def version_finder = env.BRANCH_NAME =~ /\S+?\/([0-9\.]+)\/?\S*/
   if ( version_finder.size() == 1 )
   {
      return version_finder[0][1]
   }
   throw new Exception(env.BRANCH_NAME + " not supported format for pipeline builds")
}

def isMaster()
{
   return env.BRANCH_NAME == "master"
}

def isDevelop()
{
   return ( env.BRANCH_NAME.startsWith("feature/") && env.BRANCH_NAME.endsWith("/dev") )
}

def isRelease()
{
   return env.BRANCH_NAME.startsWith("release/")
}

def isFeature()
{
   return env.BRANCH_NAME.startsWith("feature/") && !env.BRANCH_NAME.endsWith("/dev")
}

def isPublishable()
{
   if ( isDevelop() || isRelease())
   {
      return true
   }
   return false
}

def normalizeBranchName( string)
{
   return string.replaceAll("feature/[0-9.]+/","").replaceAll("/","_").replaceAll("-", "_") 
}

def rpmRelease()
{
   return env.CASUAL_RELEASE
}

def selectExtendedVersion()
{
   if ( isRelease())
      return ""

   return "-" + env.CASUAL_RELEASE
}

def totalVersion()
{
    return env.CASUAL_VERSION + selectExtendedVersion()
}

def type_of_build()
{
   if (isDevelop())
      return "beta"
   if (isFeature())
      return "alpha"
   return ""
}

def setCurrentVersionAndRelease(version)
{
   if ( isRelease())
   {
      // 1.2.buildnumber
      env.CASUAL_VERSION = version
      env.CASUAL_RELEASE = "1"
   }
   else if (isDevelop())
   {
      // 1.2-beta.buildnumber
      env.CASUAL_VERSION = retrieve_major_minor()
      env.CASUAL_RELEASE = version.split("-")[1]
   }
   else if (isFeature())
   {
      // 1.2-alpha.branchname.buildnumber
      env.CASUAL_VERSION = retrieve_major_minor()
      env.CASUAL_RELEASE = type_of_build() + "." + normalizeBranchName(env.BRANCH_NAME) + "." + env.BUILD_NUMBER
   }
   else
   {
      throw new Exception( "Branch name " + env.BRANCH_NAME + " not supported in builds")
   }
}

try
{
   if (isMaster())
   {
      currentBuild.result = 'SUCCESS'
      return
   }

   currentBuild.result = 'SUCCESS'
   slackSend color: "good", message: "Build started: ${env.JOB_NAME} - ${env.BUILD_NUMBER} (<${env.JOB_URL}|Open>)"


   node("documentation")
   {
       stage("calculate version")
       {
           checkout([
              $class: 'GitSCM',
              branches: scm.branches,
              doGenerateSubmoduleConfigurations: scm.doGenerateSubmoduleConfigurations,
              extensions: [[$class: 'CloneOption', noTags: false, shallow: false, depth: 0, reference: '']],
              userRemoteConfigs: scm.userRemoteConfigs,
           ])

           major_minor = retrieve_major_minor()
           build_type = type_of_build()
           version = sh(returnStdout: true, script: "pipeline/script/version.py $major_minor $build_type").trim()
           println version
       }
   }

   // need to be called before defines later in script
   setCurrentVersionAndRelease(version)

}
catch (Exception e)
{
   slackSend color: "danger", message: "Build failed: ${env.JOB_NAME} - ${env.BUILD_NUMBER} (<${env.JOB_URL}|Open>)"
   currentBuild.result = 'FAILURE'
   return
}

def legacy_build( name, image, content)
{
   def current_dir = pwd()
   writeFile file: 'builder.sh', text: content

   sh """
   chmod +x builder.sh
   docker pull $image
   docker run --rm -v $current_dir:/git/casual:Z $image /git/casual/builder.sh
   """
}

def build( name, image, content)
{
   def builder_image = docker.image(image)
   builder_image.pull()

   builder_image.inside {
      sh content
   }
}

def build_casual( distribution)
{
   stage('Build backend - Unittest/CodeCoverage - ' + distribution)
   {
      unstash 'frontend'

      BUILD_VERSION=totalVersion()
      CASUAL_VERSION=env.CASUAL_VERSION
      CASUAL_RELEASE=rpmRelease()
      DISTRIBUTION=distribution
      
      legacy_build( distribution + 'compile', 'casual/' + distribution + '-builder'.concat(':').concat(retrieve_major_minor()), 
"""#!/bin/bash
SCRIPT_ROOT=/git/casual

set -e

source \$SCRIPT_ROOT/casual/pipeline/script/pipeline.env

bash \$SCRIPT_ROOT/casual/pipeline/script/pre/cleanup

bash \$SCRIPT_ROOT/casual/pipeline/script/backend/$DISTRIBUTION/build $BUILD_VERSION
bash \$SCRIPT_ROOT/casual/pipeline/script/package $CASUAL_VERSION $CASUAL_RELEASE $DISTRIBUTION

bash \$SCRIPT_ROOT/casual/pipeline/script/post/cleanup
""")

      if ( distribution == 'centos' )
         step([$class: 'XUnitPublisher',
            thresholds: [[$class: 'FailedThreshold', failureThreshold: '1']],
            tools: [[$class: 'GoogleTestType', pattern: '**/report.xml']]])

      archive includes: '**/casual-middleware*.rpm'

      stash includes: '**/casual-middleware*.rpm', name: distribution + '_backend'
   }
}

try
{
   echo totalVersion()

   parallel(
   
      "frontend":
      {
         node("frontend")
         {
            deleteDir()
            stage('Build frontend')
            {
               checkout scm
               
               legacy_build( 'casualfrontend', 'casual/frontend-builder', 
"""#!/bin/bash
SCRIPT_ROOT=/git
bash \$SCRIPT_ROOT/casual/pipeline/script/frontend/build
"""
               )

               stash includes: '**/casual-webapp.zip', name: 'frontend'
            }
         }
      },
      
      "documentation":
      {
         node( "documentation")
         {
            if ( isPublishable() )
            {
               deleteDir()
               stage('Documentation')
               {
                  sh """
                  mkdir casual casual-documentation
                  """
                  dir("casual")
                  {
                     checkout scm
                  }
                  dir("casual-documentation")
                  {
                     git branch: 'master', url: 'https://github.com/casualcore/casual-documentation.git'
                     sh """
                        export CASUAL_BUILD_HOME=$WORKSPACE/casual
                        cd casual/casual-middleware
                        export CASUAL_VERSION=$CASUAL_VERSION
                        export CASUAL_RELEASE=$CASUAL_RELEASE
                        make html
                     """
                     stash includes: '**', name: 'casual-documentation'
                  }
                  dir("casual")
                  {
                     sh """
                        mkdir doxygen
                        DOCUMENT_ROOT=doxygen doxygen documentation/Doxyfile 2> /dev/null                
                     """
                     stash includes: 'doxygen/**', name: 'casual-doxygen'
                      
                  }
               }
            }
         }
      }
   )    
   
   parallel(
      "centos" :
      {
         node( "cplusplus" && "centos")
         {
            try
            { 
               deleteDir()

               stage( "checkout repos")
               {

                  dir("casual-thirdparty")
                  {
                     // casual-thirdparty
                     MAJOR_MINOR = retrieve_major_minor()
                     checkout scm: [$class: 'GitSCM', userRemoteConfigs: [[url: 'https://github.com/casualcore/casual-thirdparty.git']], branches: [[name: 'refs/tags/' + MAJOR_MINOR ]]], poll: false
                  }

                  dir("casual-make")
                  {
                     // casual-make
                     git branch: 'master', url: 'https://github.com/casualcore/casual-make.git'
                  }

                  dir("casual")
                  {
                     // casual
                     checkout scm
                  }
               }

               build_casual( 'centos')

               if ( isPublishable() )
               {
                  stage('Create container - Centos')
                  {
                     sh '''
                     docker build -t casual/middleware-centos --build-arg CASUAL_RPM=$(ls -t casual/casual-middleware-*.rpm|head -1) -f pipeline/docker/Dockerfile .
                     '''
                  }

                  stage('Publishing to dockerhub - Centos')
                  {
                     if (isRelease())
                     {
                        tag = totalVersion()
                     }
                     else
                     {
                        tag = retrieve_major_minor().concat('-beta')
                     }

                     sh """
                     docker tag casual/middleware-centos casual/middleware:$tag
                     docker push casual/middleware:$tag
                     """
                  }

                  stage('Deploy - Centos')
                  {
                     sh """
                     if [[ -d /var/lib/jenkins/git/casual/docker ]]
                     then
                        cd /var/lib/jenkins/git/casual/docker
                        ./restart.sh
                     else
                        echo "Mock deploy"
                     fi
                     """
                  } 
               }
            }
            catch (Exception e)
            {
               archive includes: '**/casual.log'
               throw e
            }
         }
      },
      "suse" :
      {
         node( "cplusplus" && "suse")
         {
            try
            { 

               deleteDir()

               stage( "checkout repos")
               {

                  dir("casual-thirdparty")
                  {
                     // casual-thirdparty
                     MAJOR_MINOR = retrieve_major_minor()
                     checkout scm: [$class: 'GitSCM', userRemoteConfigs: [[url: 'https://github.com/casualcore/casual-thirdparty.git']], branches: [[name: 'refs/tags/' + MAJOR_MINOR ]]], poll: false
                  }

                  dir("casual-make")
                  {
                     // casual-make
                     git branch: 'master', url: 'https://github.com/casualcore/casual-make.git'
                  }

                  dir("casual")
                  {
                     // casual
                     checkout scm
                  }
               }

               build_casual( 'suse')

            }
            catch (Exception e)
            {
               archive includes: '**/casual.log'
               throw e
            }
         }
      }
   )

   node( "publishing")
   {
      deleteDir()
      
      if ( isPublishable() )
      {
         stage('Tag')
         {
            deleteDir()

            dir("casual")
            {
               checkout scm

               withCredentials([usernameColonPassword(credentialsId: 'jenkins-github-tag-token', variable: 'USERPASS')])
               {
                  tag = totalVersion()
                  sh """
                  git tag -a -m "Jenkins" $tag
                  git push https://${USERPASS}@github.com/casualcore/casual.git $tag
                  """
               }
            }
         }

         stage('publish rpm')
         {
            unstash "suse_backend"
            unstash "centos_backend"

            withCredentials([usernameColonPassword(credentialsId: '628eddfb-34f4-46de-9e61-c73b86ac681e', variable: 'USERPASS')])
            {
               version=totalVersion()
               sh """
               set +x
               for filename in \$( ls casual/casual-middleware-${version}*.rpm )
               do
                  curl -X POST --user "${USERPASS}" "https://api.bitbucket.org/2.0/repositories/casualcore/casual/downloads" --form files=@"\$filename"
                  cp \$filename /var/www/release/.
               done
               """
            }
         }
      }
      if ( isPublishable())
      {
         stage('publish documentation')
         {
            deleteDir()
            dir("casual-documentation")
            {
               unstash "casual-documentation"
               unstash "casual-doxygen"
               def path = "${WORKSPACE}"
               def directories = path.split("/")
               echo "http://casual.laz.se/documentation/workspace/" + directories[-1] + "/casual-documentation/casual/casual-middleware/build/html/"
               echo "http://casual.laz.se/documentation/workspace/" + directories[-1] + "/casual-documentation/doxygen/html/"
            }
         }
      }
   }

   slackSend color: "good", message: "Build finished: ${env.JOB_NAME} - ${env.BUILD_NUMBER} (<${env.JOB_URL}|Open>)"
   currentBuild.result = 'SUCCESS'
}
catch (Exception e)
{
   slackSend color: "danger", message: "Build failed: ${env.JOB_NAME} - ${env.BUILD_NUMBER} (<${env.JOB_URL}|Open>)"
   currentBuild.result = 'FAILURE'
}

